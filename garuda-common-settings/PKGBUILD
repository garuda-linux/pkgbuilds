# Maintainer: dr460nf1r3 <dr460nf1r3 at garudalinux dot org>
# Contributor: Librewish <librewish@gmail.com>

pkgname=garuda-common-settings
pkgver="2.12.0"
_commit='848d4f2d25f6b64c934bb1a44cb468a4eaee108a'
pkgrel=1
arch=('any')
url="https://gitlab.com/garuda-linux/themes-and-settings/settings/$pkgname"
license=('GPL')
source=("https://gitlab.com/garuda-linux/themes-and-settings/settings/garuda-common-settings/-/archive/${_commit}/garuda-common-settings-${_commit}.tar.gz")
sha256sums=('SKIP')
pkgdesc='Garuda Linux common settings'
optdepends=('garuda-browser-settings: Garuda Linux Browser settings'
  'performance-tweaks: Tuning for best performance'
  'powersave-tweaks: Tuning for lower power usage'
  'pacutils: Required for garuda-inxi funstuff')
makedepends=('polkit')
conflicts=('systemd-swap' 'ipw2100-fw' 'ipw2200-fw' 'firedragon-extension-xdm-browser-monitor')
backup=(etc/sudoers.d/{editor,insults,pwfeedback})
install=$pkgname.install

package() {
  depends=('btrfsmaintenance'
    'garuda-bash-config'
    'garuda-hardware-tool'
    'garuda-health'
    'garuda-hooks'
    'garuda-icons'
    'garuda-migrations'
    'garuda-update'
    'garuda-wallpapers'
    'zram-generator'
    'systemd-oomd-defaults'
    'noto-color-emoji-fontconfig'
    'privatebin-cli')

  install -d "$pkgdir"/etc
  cp -rf "$srcdir"/$pkgname-$_commit/etc "$pkgdir"
  install -d "$pkgdir"/usr
  cp -rf "$srcdir"/$pkgname-$_commit/usr "$pkgdir"

  # Fix permissions
  chmod -R 750 "$pkgdir"/etc/sudoers.d/
  chmod -R 755 "$pkgdir"/usr/share/polkit-1/rules.d/
  chown -R root:polkitd "$pkgdir"/usr/share/polkit-1/rules.d
}
