# Maintainer: TNE <tne@garudalinux.org>

pkgname=garuda-system-maintenance
pkgver="3.0.0"
_commit='17c0a0cdd45013d1b085c41d9834ceb3416aa3eb'
pkgrel=5
pkgdesc="Automated Garuda system maintenance tool"
arch=('x86_64')
url="https://gitlab.com/garuda-linux/applications/$pkgname"
license=('GPL')
depends=('qt6-base' 'garuda-hotfixes' 'knotifications' 'kstatusnotifieritem' 'garuda-libs' 'snapper-tools')
makedepends=('qt6-tools' 'cmake' 'git' 'polkit')
source=("https://gitlab.com/garuda-linux/applications/garuda-system-maintenance/-/archive/${_commit}/garuda-system-maintenance-${_commit}.tar.gz")
sha256sums=('SKIP')

build() {
    cmake -B build -S "$pkgname-$_commit" \
        -DCMAKE_BUILD_TYPE='Release' \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -Wno-dev
    make -C build
}

package() {
    # Replaces all of garuda-settings-manager
    replaces=('garuda-settings-manager')
    conflicts=('garuda-settings-manager-kcm' 'garuda-settings-manager-notifier' 'garuda-settings-manager-knotifier' 'garuda-settings-manager-knotifier-git' 'garuda-settings-manager-notifier-git')

    make -C build DESTDIR="$pkgdir" install

    cd "$pkgname-$_commit" || exit
    install -Dm0755 update-packages "$pkgdir/usr/lib/$pkgname/update-packages"
    install -Dm0755 migrate-dr460nized "$pkgdir/usr/lib/$pkgname/migrate-dr460nized"
    install -Dm0644 $pkgname.rules "$pkgdir/usr/share/polkit-1/rules.d/$pkgname.rules"
    install -Dm0644 $pkgname@.service "$pkgdir/etc/systemd/system/$pkgname@.service"
    install -Dm0644 $pkgname.notifyrc "$pkgdir/usr/share/knotifications5/$pkgname.notifyrc"
    install -Dm0644 $pkgname.desktop "$pkgdir/etc/xdg/autostart/$pkgname.desktop"
    install -Dm0644 $pkgname-settings.desktop "$pkgdir/usr/share/applications/$pkgname-settings.desktop"
    install -Dm0644 $pkgname.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
    install -Dm0644 $pkgname-alert.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname-alert.svg"

    # Fix permissions
    chmod -R 750 "$pkgdir"/usr/share/polkit-1/rules.d/
    chown -R root:polkitd "$pkgdir"/usr/share/polkit-1/rules.d/
}
